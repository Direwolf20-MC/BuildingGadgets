buildscript {
    repositories {
        maven { url = 'https://maven.creeperhost.net' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '4.1.+', changing: true
    }
}

plugins {
    id 'java'
    id 'idea'
    id 'maven-publish'
    id "com.matthewprenger.cursegradle" version "1.4.0"
}

apply plugin: 'net.minecraftforge.gradle'

def ENV = System.getenv()
version = "${version}-build.${ENV.GITHUB_RUN_NUMBER ?: 9999}+mc${minecraft_version}"
group = 'com.direwolf20.buildinggadgets'
archivesBaseName = "buildinggadgets-${minecraft_version}"

def forge_major = forge_version.tokenize('.')[0]
java.toolchain.languageVersion = JavaLanguageVersion.of(8)

minecraft {
    mappings channel: 'snapshot', version: "${mcp_mappings}"
    // makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.

    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
            mods {
                buildinggadgets {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
            mods {
                buildinggadgets {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
            args '--mod', 'buildinggadgets', '--all', '--output', file('src/generated/resources/')
            mods {
                buildinggadgets {
                    source sourceSets.main
                }
            }
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    maven {
        url = "https://modmaven.dev/"
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"

    // JEI curse.maven:modurl-projectid:fileid
    compileOnly fg.deobf("mezz.jei:jei-${jei_version}:api")
    runtimeOnly fg.deobf("mezz.jei:jei-${jei_version}")

    // deobfCompile "team.chisel.ctm:CTM:MC${minecraft_version}-${ctm_version}:api"
    // deobfCompile "refinedstorage:refinedstorage:${rs_version}:api"
    // runtime "team.chisel.ctm:CTM:MC${minecraft_version}-${ctm_version}"
}

// Processes main only resources
processResources {
    inputs.property('version', project.version)
    inputs.property('forge_major', forge_major)

    from(sourceSets.main.resources) {
        include("META-INF/mods.toml")
        expand 'version': project.version, 'forge_major': forge_major
    }
}

jar {
    group = 'artifact'
    manifest {
        attributes(["Specification-Title"     : "BuildingGadgets",
                    "Specification-Version"   : forge_major, // We are version 1 of the modlauncher specification
                    "Implementation-Title"    : project.archivesBaseName,
                    "Implementation-Version"  : project.version,
                    "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")],)
    }

    from sourceSets.main.output
    dependsOn classes
}

task deobfJar(type: Jar) {
    from(sourceSets.main.output)
    archiveName = "${baseName}-${version}-deobf.${extension}"
}

task sourcesJar(type: Jar) {
    from(sourceSets.main.allSource)
    archiveName = "${baseName}-${version}-sources.${extension}"
}

artifacts {
    archives deobfJar
    archives sourcesJar
}

jar.finalizedBy('reobfJar')

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            artifactId = project.archivesBaseName.toLowerCase()
            artifact(deobfJar) {
                classifier 'deobf'
            }
        }
    }

    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/Direwolf20-MC/BuildingGadgets"
            credentials {
                username = ENV.GITHUB_ACTOR
                password = ENV.GITHUB_TOKEN
            }
        }
    }
}

if (ENV.CURSE_TOKEN) {
    curseforge {
        apiKey = ENV.CURSE_TOKEN
        project {
            id = "298187"
            releaseType = "release"
            addGameVersion "${minecraft_version}"
            addGameVersion "Forge"
            addGameVersion "Java 8"
            mainArtifact(jar) {
                relations {
                    optionalDependency 'charging-gadgets'
                }
            }
            changelog = file('CHANGELOG.md')
            changelogType = 'markdown'
        }
    }
}
